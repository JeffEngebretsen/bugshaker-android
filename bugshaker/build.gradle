buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

/**
 * Holds the information associated with the library needed for the
 * bintray plugin to publish the artifact
 */
class LibraryInfo {
    static Integer versionMajor = 2
    static Integer versionMinor = 1
    static Integer versionPatch = 2

    static String artifactId = 'bugshaker'
    static String groupId = 'com.jeffengebretsen'
    static String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    static Integer versionCode = versionMajor * 10_000 + versionMinor * 1_000 + versionPatch * 100
}

static def getLibraryInfo() {
    return new LibraryInfo()
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode getLibraryInfo().versionCode
        versionName getLibraryInfo().versionName
    }

    buildTypes {
        debug { // Required so that dependencies are properly protected when building debug example build type.
            consumerProguardFiles 'proguard/rxjava.pro', 'proguard/bugshaker.pro', 'proguard/v7support.pro'
        }

        release {
            consumerProguardFiles 'proguard/rxjava.pro', 'proguard/bugshaker.pro', 'proguard/v7support.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

//configurations {
//    releaseImplementation {
//        canBeConsumed = true
//    }
//}

dependencies {
    def supportLibraryVersion = '27.0.2'
    def seismicVersion = '1.0.2'

    api "com.squareup:seismic:$seismicVersion"
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.4'

    implementation('com.google.android.gms:play-services-maps:11.4.2') { ext.optional = true }
    implementation("com.android.support:appcompat-v7:${supportLibraryVersion}") { ext.optional = true }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.24.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

afterEvaluate {
    apply from: 'bintray.gradle'
}
